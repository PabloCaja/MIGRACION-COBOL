      ******************************************************************
      *     I D E N T I F I C A T I O N  D I V I S I O N               *
      ******************************************************************
       IDENTIFICATION DIVISION.
      **
       PROGRAM-ID.   EXTRCLI.
       AUTHOR.       CAJA.
       DATE-WRITTEN. 28/09/2022.
       DATE-COMPILED.
      *
      ******************************************************************
      *     ENVIRONMENT DIVISION                                       *
      ******************************************************************
       ENVIRONMENT DIVISION.
      *
       CONFIGURATION SECTION.
         SOURCE-COMPUTER.  IBM-3090.
         OBJECT-COMPUTER.  IBM-3090.
      *
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *
       INPUT-OUTPUT SECTION.
      * SE DECLARAN LOS FICHEROS DE ENTRADA Y SALIDA
      *
       FILE-CONTROL.
      * OBLIGATORIO, PARA DECLARAR LOS FICHEROS DE ENTRADA Y SALIDA
      *
      ******************************************************************
      *     DATA DIVISION                                              *
      ******************************************************************
       DATA DIVISION.
      *
       FILE SECTION.
      *
      ******************************************************************
      *     F I L E  S E C T I O N                                     *
      ******************************************************************
      *
      *
      ******************************************************************
      *     W O R K I N G   S T O R A G E                              *
      ******************************************************************
       WORKING-STORAGE SECTION.
      * LUGAR DONDE DECLARAMOS CONSTANTES, VARIABLES, COPYS,SWITCHES,...
      * CONSTANTES ALFANUMERICAS
       01 CA-CONSTANTES-ALF.
          05 CA-RUT                    PIC X(08) VALUE 'EXTRCLI'.
          05 CA-L                      PIC X(01) VALUE 'L'.
          05 CA-R                      PIC X(01) VALUE 'R'.
          05 CA-S                      PIC X(01) VALUE 'S'.
          05 CA-00                     PIC X(02) VALUE '00'.
          05 CA-99                     PIC X(02) VALUE '99'.
          05 CA-PARR-PRO    PIC X(12) VALUE '2000-PROCESO'.
          05 CA-DESC-ERR    PIC X(12) VALUE 'ERROR SELECT'.
      *
       01 SW-SWITCHES.
          05 SW-FIN-CURSOR             PIC X(01).
             88 SI-FIN-CURSOR          VALUE 'S'.
             88 NO-FIN-CURSOR          VALUE 'N'.
      *
       01 WK-CONTADORES.
          05 CONT-LEIDOS               PIC 9(3).
      *
       01 WK-SQLCODE                   PIC -999.
      *---------------- SQLCA ------------------------
           EXEC SQL
               INCLUDE SQLCA
           END-EXEC.
      *---------------- DCLGEN -----------------------
           EXEC SQL
               INCLUDE TBCLPEPI
           END-EXEC.
      *-------------- DEFINIMOS LOS CURSORES ---------
           EXEC SQL
               DECLARE CURSOR-LIST CURSOR FOR
                  SELECT DNI_CL
                        ,NOMBRE_CL
                        ,APELLIDO_1
                        ,APELLIDO_2
                        ,CLASE_VIA
                        ,NOMBRE_VIA
                        ,NUMERO_VIA
                        ,COD_POSTAL
                        ,CIUDAD
                        ,TELEFONO
                        ,OBSERVACIONES
      *
                    FROM CLIENTES_PEPITO_SEG
                   ORDER BY DNI_CL
           END-EXEC.
      *
      *---------------- CURSOR DE REPAG1 -----------
           EXEC SQL
               DECLARE REPAG1 CURSOR FOR
                  SELECT DNI_CL
                        ,NOMBRE_CL
                        ,APELLIDO_1
                        ,APELLIDO_2
                        ,CLASE_VIA
                        ,NOMBRE_VIA
                        ,NUMERO_VIA
                        ,COD_POSTAL
                        ,CIUDAD
                        ,TELEFONO
                        ,OBSERVACIONES
      *
                    FROM CLIENTES_PEPITO_SEG
                   WHERE DNI_CL         > :TB-DNI-CL
                   ORDER BY DNI_CL
           END-EXEC.
      *
      ******************************************************************
      *     L I N K A G E   S E C T I O N                              *
      ******************************************************************
       LINKAGE SECTION.
      * ESTRUCTURA DE COMUNICACION DE MI RUTINA
       COPY CPYCLIA.
      *
      ******************************************************************
      *     PROCEDURE DIVISION                                         *
      ******************************************************************
       PROCEDURE DIVISION USING CPYCLIA.
      *
           PERFORM 1000-INICIO
              THRU 1000-INICIO-EXIT
      *
           PERFORM 2000-PROCESO
              THRU 2000-PROCESO-EXIT
      *
           PERFORM 3000-FIN
              THRU 3000-FIN-EXIT.
      *
      ******************************************************************
      *     1000-INICIO                                                *
      ******************************************************************
       1000-INICIO.
      *
           INITIALIZE SALIDA
                      ERRORES
                      SALIDA-CONTROL
                      DCLCLIENTES-PEPITO-SEG

           MOVE CA-00              TO RETORNO
           MOVE 1                  TO CONT-LEIDOS
           MOVE CA-S               TO MAS-DATOS
      *
           SET NO-FIN-CURSOR       TO TRUE
      *----VALIDACION DE LOS CAMPOS DE ENTRADA-------------------------*
           PERFORM 1100-VALIDAR-OPCION
              THRU 1100-VALIDAR-OPCION-EXIT
      *
           PERFORM 1200-VALIDAR-NUM-ELEM-E
              THRU 1200-VALIDAR-NUM-ELEM-E-EXIT
      *
           .
       1000-INICIO-EXIT.
           EXIT.
      *
      ******************************************************************
      *     1100-VALIDAR-OPCION                                        *
      ******************************************************************
       1100-VALIDAR-OPCION.
      *
           IF OPCION NOT EQUAL CA-L
           AND OPCION NOT EQUAL CA-R
              DISPLAY 'OPCION INCORRECTA'
              MOVE '10'                   TO RETORNO
              MOVE '11'                   TO SUBRETORNO
              MOVE 'VALIDACION'           TO ACCION
              MOVE SPACES                 TO TABLA
              MOVE '1100-VALIDAR-OPCION'  TO PARRAFO
              MOVE CA-RUT                 TO NOMRUTINA

              PERFORM 3000-FIN
                 THRU 3000-FIN-EXIT

           END-IF
      *
           .
       1100-VALIDAR-OPCION-EXIT.
           EXIT.
      *
      ******************************************************************
      *     1200-VALIDAR-NUM-ELEM-E                                    *
      ******************************************************************
       1200-VALIDAR-NUM-ELEM-E.
      *
           IF NUM-ELEM-E < 0
              DISPLAY 'NUM-ELEM-E INCORRECTO'
              MOVE '10'                   TO RETORNO
              MOVE '01'                   TO SUBRETORNO
              MOVE 'VALIDACION'           TO ACCION
              MOVE SPACES                 TO TABLA
              MOVE '1200-VALIDAR-NUM-ELEM-E'     TO PARRAFO
              MOVE CA-RUT                 TO NOMRUTINA

              PERFORM 3000-FIN
                 THRU 3000-FIN-EXIT

           END-IF
      *
           IF NUM-ELEM-E > 3
              DISPLAY 'NUM-ELEM-E INCORRECTO'
              MOVE '10'                   TO RETORNO
              MOVE '02'                   TO SUBRETORNO
              MOVE 'VALIDACION'           TO ACCION
              MOVE SPACES                 TO TABLA
              MOVE '1200-VALIDAR-NUM-ELEM-E'     TO PARRAFO
              MOVE CA-RUT                 TO NOMRUTINA

              PERFORM 3000-FIN
                 THRU 3000-FIN-EXIT

           END-IF
      *
           .
       1200-VALIDAR-NUM-ELEM-E-EXIT.
           EXIT.
      *
      ******************************************************************
      *     2000-PROCESO                                               *
      ******************************************************************
       2000-PROCESO.
      *
           DISPLAY 'VALIDO OPCION'
           EVALUATE OPCION
               WHEN CA-L
                    DISPLAY 'OPCION -L- '
                    PERFORM 2100-ABRIR-CURSOR-LIST
                       THRU 2100-ABRIR-CURSOR-LIST-EXIT

                    PERFORM 2200-LEER-CURSOR-LIST
                       THRU 2200-LEER-CURSOR-LIST-EXIT
                      UNTIL CONT-LEIDOS > NUM-ELEM-E
                         OR SI-FIN-CURSOR

                    PERFORM 2300-CERRAR-CURSOR-LIST
                       THRU 2300-CERRAR-CURSOR-LIST-EXIT
               WHEN CA-R
                    MOVE DNI-CL-REP           TO TB-DNI-CL

                    PERFORM 2400-ABRIR-CURSOR-REPAG1
                       THRU 2400-ABRIR-CURSOR-REPAG1-EXIT

                    PERFORM 2500-LEER-CURSOR-REPAG1
                       THRU 2500-LEER-CURSOR-REPAG1-EXIT
                      UNTIL CONT-LEIDOS > NUM-ELEM-E
                         OR SI-FIN-CURSOR

                    PERFORM 2600-CERRAR-CURSOR-REPAG1
                       THRU 2600-CERRAR-CURSOR-REPAG1-EXIT
           END-EVALUATE
      *
           .
       2000-PROCESO-EXIT.
           EXIT.
      *
      ******************************************************************
      *     2100-ABRIR-CURSOR-LIST
      *
      ******************************************************************
       2100-ABRIR-CURSOR-LIST.
      *
           SET NO-FIN-CURSOR TO TRUE
      *
           DISPLAY 'ABRO CURSOR DE LISTA'
           EXEC SQL
               OPEN CURSOR-LIST
           END-EXEC.
      *
           EVALUATE SQLCODE
              WHEN 0
                   CONTINUE
              WHEN OTHER
                   MOVE CA-99                  TO RETORNO
                   MOVE CA-99                  TO SUBRETORNO
                   MOVE 'OPEN      '           TO ACCION
                   MOVE 'CLIENTES_PEPITO_SEG'     TO TABLA
                   MOVE '2100-ABRIR-CURSOR-LIST'             TO PARRAFO
                   MOVE CA-RUT                 TO NOMRUTINA

                   PERFORM 3000-FIN
                      THRU 3000-FIN-EXIT
           END-EVALUATE
      *
           .
       2100-ABRIR-CURSOR-LIST-EXIT.
           EXIT.
      *
      ******************************************************************
      *     2200-LEER-CURSOR-LIST                                      *
      ******************************************************************
       2200-LEER-CURSOR-LIST.
      *
           DISPLAY 'HAGO FETCH AL CURSOR DE -L-'
      *
           EXEC SQL
              FETCH CURSOR-LIST
               INTO :TB-DNI-CL
                   ,:TB-NOMBRE-CL
                   ,:TB-APELLIDO-1
                   ,:TB-APELLIDO-2
                   ,:TB-CLASE-VIA
                   ,:TB-NOMBRE-VIA
                   ,:TB-NUMERO-VIA
                   ,:TB-COD-POSTAL
                   ,:TB-CIUDAD
                   ,:TB-TELEFONO
                   ,:TB-OBSERVACIONES

           END-EXEC.
      *
           EVALUATE SQLCODE
              WHEN 0
                   IF CONT-LEIDOS <= NUM-ELEM-E
                      PERFORM 2250-INFORMAR-SALIDA
                         THRU 2250-INFORMAR-SALIDA-EXIT
                   END-IF
              WHEN 100
                   SET SI-FIN-CURSOR           TO TRUE
                   MOVE 'N'                    TO MAS-DATOS
              WHEN OTHER
                   MOVE CA-99                  TO RETORNO
                   MOVE CA-99                  TO SUBRETORNO
                   MOVE 'FETCH     '           TO ACCION
                   MOVE 'CLIENTES_PEPITO_SEG' TO TABLA
                   MOVE '2200-LEER-CURSOR-LIST ' TO PARRAFO
                   MOVE CA-RUT                 TO NOMRUTINA

                   PERFORM 3000-FIN
                      THRU 3000-FIN-EXIT
           END-EVALUATE
      *
           .
       2200-LEER-CURSOR-LIST-EXIT.
           EXIT.
      *
      ******************************************************************
      *     2300-CERRAR-CURSOR-LIST                                    *
      ******************************************************************
       2300-CERRAR-CURSOR-LIST.
      *
           EXEC SQL
              CLOSE CURSOR-LIST
           END-EXEC.
      *
           EVALUATE SQLCODE
              WHEN 0
                   CONTINUE
              WHEN OTHER
                   MOVE CA-99                  TO RETORNO
                   MOVE CA-99                  TO SUBRETORNO
                   MOVE 'CLOSE     '           TO ACCION
                   MOVE 'CLIENTES_PEPITO_SEG' TO TABLA
                   MOVE '2300-CERRAR-CURSOR-LIST' TO PARRAFO
                   MOVE CA-RUT                 TO NOMRUTINA

                   PERFORM 3000-FIN
                      THRU 3000-FIN-EXIT
           END-EVALUATE
      *
           .
       2300-CERRAR-CURSOR-LIST-EXIT.
           EXIT.
      *
      ******************************************************************
      *     2250-INFORMAR-SALIDA                                       *
      ******************************************************************
       2250-INFORMAR-SALIDA.
      *
      *-- INFORMAR EL OCCURS DE SALIDA CON LOS DATOS DEL FETCH
            MOVE TB-DNI-CL         TO DNI-CL(CONT-LEIDOS)
            MOVE TB-NOMBRE-CL      TO NOMBRE-CL(CONT-LEIDOS)
            MOVE TB-APELLIDO-1     TO APELLIDO-1(CONT-LEIDOS)
            MOVE TB-APELLIDO-2     TO APELLIDO-2(CONT-LEIDOS)
            MOVE TB-CLASE-VIA      TO CLASE-VIA(CONT-LEIDOS)
            MOVE TB-NOMBRE-VIA     TO NOMBRE-VIA(CONT-LEIDOS)
            MOVE TB-NUMERO-VIA     TO NUMERO-VIA(CONT-LEIDOS)
            MOVE TB-COD-POSTAL     TO COD-POSTAL(CONT-LEIDOS)
            MOVE TB-CIUDAD         TO CIUDAD(CONT-LEIDOS)
            MOVE TB-TELEFONO       TO TELEFONO(CONT-LEIDOS)
            MOVE TB-OBSERVACIONES  TO OBSERVACIONES(CONT-LEIDOS)
      *
            MOVE TB-DNI-CL         TO DNI-CL-REP
      *
            ADD 1                     TO CONT-LEIDOS
            ADD 1                     TO NUM-ELEM-S
      *
           .
       2250-INFORMAR-SALIDA-EXIT.
           EXIT.
      *
      ******************************************************************
      *     2400-ABRIR-CURSOR-REPAG1                                   *
      ******************************************************************
       2400-ABRIR-CURSOR-REPAG1.
      *
           SET NO-FIN-CURSOR TO TRUE
      *
           EXEC SQL
               OPEN REPAG1
           END-EXEC.
      *
           EVALUATE SQLCODE
              WHEN 0
                   CONTINUE
              WHEN OTHER
                   MOVE CA-99                  TO RETORNO
                   MOVE CA-99                  TO SUBRETORNO
                   MOVE 'OPEN      '           TO ACCION
                   MOVE 'CLIENTES_PEPITO_SEG'     TO TABLA
                   MOVE '2400-ABRIR-CURSOR-REPAG1'    TO PARRAFO
                   MOVE CA-RUT                 TO NOMRUTINA

                   PERFORM 3000-FIN
                      THRU 3000-FIN-EXIT
           END-EVALUATE
      *
           .
       2400-ABRIR-CURSOR-REPAG1-EXIT.
           EXIT.
      *
      ******************************************************************
      *     2500-LEER-CURSOR-REPAG1                                    *
      ******************************************************************
       2500-LEER-CURSOR-REPAG1.
      *
           EXEC SQL
              FETCH REPAG1
               INTO :TB-DNI-CL
                   ,:TB-NOMBRE-CL
                   ,:TB-APELLIDO-1
                   ,:TB-APELLIDO-2
                   ,:TB-CLASE-VIA
                   ,:TB-NOMBRE-VIA
                   ,:TB-NUMERO-VIA
                   ,:TB-COD-POSTAL
                   ,:TB-CIUDAD
                   ,:TB-TELEFONO
                   ,:TB-OBSERVACIONES

           END-EXEC.
      *
           EVALUATE SQLCODE
              WHEN 0
                   IF CONT-LEIDOS <= NUM-ELEM-E
                      PERFORM 2250-INFORMAR-SALIDA
                         THRU 2250-INFORMAR-SALIDA-EXIT
                   END-IF
              WHEN 100
                   SET SI-FIN-CURSOR           TO TRUE
                   MOVE 'N'                    TO MAS-DATOS
              WHEN OTHER
                   MOVE CA-99                  TO RETORNO
                   MOVE CA-99                  TO SUBRETORNO
                   MOVE 'FETCH     '           TO ACCION
                   MOVE 'CLIENTES_PEPITO_SEG'     TO TABLA
                   MOVE '2500-LEER-CURSOR-REPAG1'     TO PARRAFO
                   MOVE CA-RUT                 TO NOMRUTINA

                   PERFORM 3000-FIN
                      THRU 3000-FIN-EXIT
           END-EVALUATE
      *
           .
       2500-LEER-CURSOR-REPAG1-EXIT.
           EXIT.
      *
      ******************************************************************
      *     2600-CERRAR-CURSOR-REPAG1                                  *
      ******************************************************************
       2600-CERRAR-CURSOR-REPAG1.
      *
           EXEC SQL
              CLOSE REPAG1
           END-EXEC.
      *
           EVALUATE SQLCODE
              WHEN 0
                   CONTINUE
              WHEN OTHER
                   MOVE CA-99                  TO RETORNO
                   MOVE CA-99                  TO SUBRETORNO
                   MOVE 'CLOSE     '           TO ACCION
                   MOVE 'CLIENTES_PEPITO_SEG'  TO TABLA
                   MOVE '2600-CERRAR-CURSOR-REPAG1'   TO PARRAFO
                   MOVE CA-RUT                 TO NOMRUTINA

                   PERFORM 3000-FIN
                      THRU 3000-FIN-EXIT
           END-EVALUATE
      *
           .
       2600-CERRAR-CURSOR-REPAG1-EXIT.
           EXIT.
      *
      *
      ******************************************************************
      *     3000-FIN                                                   *
      ******************************************************************
       3000-FIN.
      *
           GOBACK.
       3000-FIN-EXIT.
           EXIT.
      *

